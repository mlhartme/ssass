#
# Copyright 1&1 Internet AG, http://www.1and1.org
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation; either version 2 of the License,
# or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#


/*

 CCS 2.1 grammar from http://www.w3.org/TR/CSS2/grammar.html.

 Manual fixes:
 * in "media_list" rule: changed "COMMA" to ","
 * removed BAD_STRING and BAD_URI rules (with sub-rules) because Mork has no feature to report them as errors
 * removed BAD_COMMENT because Mork has no feature to accept it at EOF
 * various helper symbols (starting with _) added to add semantics
 * additional symbols for sass features, marked with a tailing _
   * variables (variable_, reference_)
   * nesting (ssass_declaration_, nested_property)
 * conflict resolution
   * replaced S* with Sp+ and S+ with Sp, where SP ::= s+
   * changed declaration to require Sp after ":". This is a language change, but resolves my conflicts
*/

[PARSER]
     stylesheet ::= ( CHARSET_SYM STRING ";" )? (Sp|CDO|CDC)*
                    ( imprt ( CDO Sp? | CDC Sp? )* )*
                    ( (ruleset | media | page | variable_ | mixin_) (CDO Sp? | CDC Sp?)* )* ;
          imprt ::= IMPORT_SYM Sp? (STRING|URI) Sp? media_list? ";" Sp? ;
          media ::= MEDIA_SYM Sp? media_list "{" Sp? ruleset* "}" Sp? ;
     media_list ::= medium ( "," Sp? medium )* ;
         medium ::= IDENT Sp? ;
           page ::= PAGE_SYM Sp? pseudo_page? "{" Sp? ssass_declaration_? ( ";" Sp? ssass_declaration_? )* "}" Sp? ;
    pseudo_page ::= ":" IDENT Sp? ;
       operator ::= _slash | _comma ;
         _slash ::= "/" Sp? ;
         _comma ::= "," Sp? ;
     combinator ::= _plus | _gt ;
          _plus ::= "+" Sp? ;
            _gt ::= ">" Sp? ;
 unary_operator ::= _unary_minus | _unary_plus ;
    _unary_plus ::= "+" ;
   _unary_minus ::= "-" ;
       property ::= IDENT ;
        ruleset ::= selector ( "," Sp? selector )* "{" Sp? ssass_declaration_? ( ";" Sp? ssass_declaration_? )* "}" Sp? ;
       selector ::= simple_selector ( combinator selector | Sp ( combinator? selector )? )? ;
simple_selector ::= element_name ( _hash | class | attrib | pseudo )*
                  | ( _hash | class | attrib | pseudo )+ ;
          _hash ::= HASH ;
          class ::= "." IDENT ;
   element_name ::= IDENT | "*" ;
         attrib ::= "[" Sp? IDENT Sp? ( ( _eq | _includes | _dashmatch ) Sp? ( IDENT | STRING ) Sp? )? "]" ;
            _eq ::= "=" ;
      _includes ::= INCLUDES ;
     _dashmatch ::= DASHMATCH ;
         pseudo ::= ":" ( _pseudo1 | _pseudo2 ) ;
       _pseudo1 ::= IDENT ;
       _pseudo2 ::= FUNCTION Sp? (IDENT Sp?)? ")" ;
    declaration ::= property Sp? ":" Sp expr prio? ;
           prio ::= IMPORTANT_SYM Sp? ;
           expr ::= term ( operator? term )* ;
           term ::= _term1 | _term2 ;
         _term1 ::= unary_operator? ( _number Sp? | _percentage Sp? | _length Sp? | _ems Sp? | _exs Sp? | _angle Sp? | _time Sp? | _freq Sp? | reference_ Sp? );
         _term2 ::= _string Sp? | _ident Sp? | _uri Sp? | hexcolor | function ;
        _number ::= NUMBER ;
    _percentage ::= PERCENTAGE ;
        _length ::= LENGTH ;
           _ems ::= EMS ;
           _exs ::= EXS ;
         _angle ::= ANGLE ;
          _time ::= TIME ;
          _freq ::= FREQ ;
        _string ::= STRING ;
         _ident ::= IDENT ;
           _uri ::= URI ;
       function ::= FUNCTION Sp? expr ")" Sp? ;
       hexcolor ::= HASH Sp? ;

# sass additions

ssass_declaration_ ::= declaration | ruleset | nested_property_ | include_ ;
  nested_property_ ::= property Sp? ":" Sp "{" Sp? ssass_declaration_? ( ";" Sp? ssass_declaration_? )* "}" Sp? ;
         variable_ ::= "$" IDENT Sp? ":" Sp? expr ";" Sp?;
        reference_ ::= "$" IDENT ;
            mixin_ ::= "@mixin" Sp? IDENT Sp? "{" Sp? ssass_declaration_? ( ";" Sp? ssass_declaration_? )* "}" Sp?;
          include_ ::= "@include" Sp? IDENT ;

[SCANNER]
    white = comment ;

             Sp ::= s+;
            CDO ::= "<!--";
            CDC ::= "-->";
       INCLUDES ::= "~=";
      DASHMATCH ::= "|=";
         STRING ::= string;
          IDENT ::= ident;
           HASH ::= "#" name;

     IMPORT_SYM ::= "@import";
       PAGE_SYM ::= "@page";
      MEDIA_SYM ::= "@media";
    CHARSET_SYM ::= "@charset ";
  IMPORTANT_SYM ::= "!" (w | comment)* "important" ;
            EMS ::= num "em";
            EXS ::= num "ex";
         LENGTH ::= num ("px" | "cm" | "mm" | "in" | "pt" | "pc");
          ANGLE ::= num ("deg" | "rad" | "grad");
           TIME ::= num ("ms" | "s");
           FREQ ::= num ("hz" | "khz");
    # TODO: not used:
    # DIMENSION ::= num ident;
     PERCENTAGE ::= num "%";
         NUMBER ::= num;

            URI ::= "url(" w string w ")";
            URI ::= "url(" w url w ")";
       FUNCTION ::= ident "(";

              h ::= 'A'..'Z' | 'a'..'z' | '0' .. '9' ;
       nonascii ::= (160 .. 255);
        unicode ::= '\\' h h? h? h? h? h? ('\r' '\n' | ' ' | '\t' | '\r' | '\n' | '\f' )? ;
         escape ::= unicode | '\\' ('\r' | '\n' | '\f' | '0'..'9' | 'A'..'F' | 'a'..'f')! ;
        nmstart ::= ('_' | 'A' .. 'Z' | 'a'..'z' | nonascii | escape );
         nmchar ::= ('_' | 'A' .. 'Z' | 'a'..'z' | '0'..'9' | '-' | nonascii | escape );
        string1 ::= '"' (('\n' | '\r' | '\f' | '"')! | '\\' nl | escape)* '"';
        string2 ::= '\'' (('\n' | '\r' | '\f' | '\'')! | '\\' nl | escape)* '\'';
        comment ::= '/' '*' '*'!* '*'+ (('/' | '*')! '*'!* '*'+)* '/' ;
          ident ::= '-'? nmstart nmchar* ;
           name ::= nmchar+ ;
            num ::= ('0'..'9')+ | ('0'..'9')* '.' ('0'..'9')+;
         string ::= string1 | string2 ;

            url ::= ("!" | "#" | "$" | "%" | "&" | '*'..'~' | nonascii | escape )*;

              s ::= (' ' | '\t' | '\r' | '\n' | '\f')+ ;
              w ::= s? ;
             nl ::= '\n' | '\r' '\n' | '\r' | '\f' ;
